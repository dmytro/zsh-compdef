#compdef ack
#
# First attempt at ZSH completion script for ack of App::Ack 1.96.
# Install as _ack in a $fpath directory.
#
# 'color' was selected for shorter option strings, sorry colour fans.
#
# XXX probably can add more excludes between various incompatible options
# XXX probably need option sets, e.g. w/ --print0 only -f, -g, -l, -L or -c

local ret=1

_arguments -s \
  '(-m --max-count)-1[there can be only one match]' \
  '(-A --after-context)'{-A,--after-context=}'[print NUM lines trailing context]:number:' \
  '(-B --before-context)'{-B,--before-context=}'[print NUM lines leading context]:number:' \
  '(-a --all-types)'{-a,--all-types}'[all file types searched]' \
  '(--nobreak)--break[print break between results from different files]' \
  '(--nocolor --nocolour --colour)--color[highlight the matching text]' \
  '(--nocolor --nocolour)--color-filename=[set color for filenames]:color:' \
  '(--nocolor --nocolour)--color-match=[set color for matches]:color:' \
  '(--nocolor --nocolour)--color-lineno=[set color for line numbers]:color:' \
  '--column[show column number of the first match]' \
  '(-C --context)'{-C,--context=}'[print NUM lines (default 2) of context]:number:' \
  '(-c --count)'{-c,--count}'[show number of lines matching per file]' \
  '(--match -g)-f[only print files found (no PATTERN)]' \
  '(-l --files-with-matches -L --files-without-matches)'{-l,--files-with-matches}'[only print filenames containing matches]' \
  '(-l --files-with-matches -L --files-without-matches)'{-L,--files-without-matches}'[only print filenames with no matches]' \
  '--flush[flush output immediately]' \
  '(--nofollow)--follow[follow symlinks]' \
  '-G[only search files matching REGEX]:regex:' \
  '(-f --match)-g[as -f but only files matching REGEX]:regex:' \
  '(--nogroup)--group[same as --heading --break]' \
  '(--noheading)--heading[print filename heading above results from each file]' \
  '(- *)--help[display help]' \
  '(-i --ignore-case)'{-i,--ignore-case}'[ignore case in PATTERN]' \
  '--ignore-dir=[ignore specified dir]:directory:_directories' \
  '--invert-file-match[print/search handle files that do not match -g/-G]' \
  '(-v --invert-match)'{-v,--invert-match}'[invert match]' \
  '--line=[only print line(s) NUM of each file]:number:' \
  '(-Q --literal)'{-Q,--literal}'[quote all metachars in PATTERN]' \
  '(- *)--man[display manual]' \
  '(-f -g)--match=[specify PATTERN explicitly]:pattern:' \
  '(-1 -m --max-count)'{-m,--max-count=}'[stop search in each file after NUM matches]:number:' \
  '(--break)--nobreak[omit break between different file results]' \
  '(--color --colour --nocolour)--nocolor[do not highlight matching text]' \
  '--noenv[ignore env and ackrc]' \
  '(--follow)--nofollow[no not follow symlinks]' \
  '(--group)--nogroup[same as --noheading --nobreak]' \
  '(--heading)--noheading[omit filename heading above results]' \
  '--noignore-dir=[unignore specified dir]:directory:_directories' \
  '--nopager[do not send output thru pager]' \
  '(--perl)--noperl[exclude Perl files]' \
  '(-r -R --recurse -n --no-recurse)'{-n,--norecurse}'[no subdir recursion]' \
  '(--smart-case)--nosmart-case[do not ignore case if PATTERN has no uppercase]' \
  '-o[show only part of a line matching PATTERN]' \
  '--output=[output the eval of expr for each line]:expr:' \
  '(--no-pager)--pager=[pipes output through COMMAND]:command:' \
  '--passthru[print all lines]' \
  '(--noperl)--perl[include only Perl files]' \
  '--print0[print null byte as separator]' \
  '(-r -R --recurse -n --no-recurse)'{-r,-R,--recurse}'[recurse into subdirs]' \
  '--show-types[show which types each file has]' \
  '(-i --ignore-case --nosmart-case)--smart-case[ignore case only if PATTERN has no uppercase]' \
  '--sort-files[sort found files lexically]' \
  '(- *)--thpppt[Bill the Cat]' \
  '--type=[include only files of TYPE]:type:' \
  '--type-add=[append to TYPE=.ext,.ext2,... type set]:typeset:' \
  '--type-set=[specify TYPE=.ext,.ext2,... type set]:typeset:' \
  '(-u --unrestricted)'{-u,--unrestricted}'[all files and dirs searched]' \
  '(- *)--version[display version]' \
  '(-H --with-filename -h --no-filename)'{-H,--with-filename}'[print filename for each match]' \
  '(-H --with-filename -h --no-filename)'{-h,--no-filename}'[suppress filename prefix on output]' \
  '(-w --word-regexp)'{-w,--word-regexp}'[match only whole words]' \
  '1:pattern:' \
  '*:files:_files' \
  && ret=0

return $ret
